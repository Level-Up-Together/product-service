buildscript {
    ext {
        restdocsApiSpecVersion = '0.17.1'
        queryDslVersion = '5.0.0'
    }
    dependencies {
        classpath 'com.netflix.graphql.dgs.codegen:graphql-dgs-codegen-gradle:6.1.4'
        classpath 'au.com.dius.pact.provider:gradle:4.6.5'
    }
}

plugins {
    id 'org.springframework.boot' version '3.4.5' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}" apply false
    id 'org.hidetake.swagger.generator' version '2.18.2' apply false
    id "com.netflix.dgs.codegen" version "6.1.4" apply false
}

ext {
    set('springCloudVersion', "2024.0.0")
    lutPlatformVersion = findProperty('lutPlatformVersion') ?: '1.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = 'io.pinkspider'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Level-Up-Together/level-up-together-platform")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:8.0.0"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    // QueryDSL 공통 설정
    def querydslDir = "${buildDir}/generated/querydsl"

    sourceSets {
        main.java.srcDirs += [querydslDir]
    }

    tasks.withType(JavaCompile).configureEach {
        options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
        options.compilerArgs.add('-parameters')
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    clean.doLast {
        file(querydslDir).deleteDir()
    }
}
